buildscript {
    repositories {
        mavenCentral()
        maven { url = "http://files.minecraftforge.net/maven" }
        maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
    }
    dependencies { classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT' }
}
plugins {
    id 'io.franzbecker.gradle-lombok' version '1.7'
	id 'com.matthewprenger.cursegradle' version '1.0.7'
}

apply plugin: 'net.minecraftforge.gradle.forge'

ext.buildnumber = 0
if (System.getenv('BUILD_NUMBER') != null)
    project.buildnumber = System.getenv('BUILD_NUMBER')
else
    logger.lifecycle "No build number detected, setting to 0"

group = "tterrag.colorblindhelper"
archivesBaseName = "ColorblindHelper"
version = "${minecraft_version}-${mod_version}.${buildnumber}"

if (!project.curse_type.equals('release'))
    version += "_${project.curse_type}"

minecraft {
    version = "${minecraft_version}-${forge_version}"
    runDir = 'run'
    
    mappings = 'snapshot_20171212'

    replace "@VERSION@", project.version
    replaceIn "ColorblindHelper.java"
}

configurations {
    compile.extendsFrom exportedCompile
}

repositories {
    maven { url = "http://maven.tterrag.com" }
}

dependencies {
    deobfCompile "com.enderio.core:EnderCore:${endercore_version}"
}

lombok {
    version = "1.16.10"
    sha256 = null // skip
}

import io.franzbecker.gradle.lombok.task.DelombokTask

task delombok(type: DelombokTask) {
    args("src/main/java", "-d", "build/sources/delomboked/java")
}

tasks.eclipse.dependsOn installLombok

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include '**/*.info'
        include '**/*.properties'

        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.info'
        exclude '**/*.properties'
    }
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

build.dependsOn sourceJar

curseforge {
    if (project.hasProperty('curseforge_key')) {
        apiKey = project.curseforge_key
    }

    project {
        id = project.curse_projectId
        changelog = System.getenv('CHANGELOG') == null || System.getenv('CHANGELOG').equals('none') ? getChangelog() : System.getenv('CHANGELOG')
        changelogType = 'html'
        releaseType = project.curse_type
		addGameVersion '1.10.2'
        mainArtifact(jar) {
            displayName = "ColorblindHelper - ${version}"
        }
    }
}
